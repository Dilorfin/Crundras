_Program = {Statement}.

Statement = InputStatement | OutputStatement | CompoundStatement | SelectionStatement | IterationStatement | DeclarationStatement | AssignmentStatement | JumpStatement | LabelStatement.
SelectionStatement = "if" '(' Expression ')' Statement.
IterationStatement = "for" AssignmentExpression "to" Expression "by" Expression "while" '(' Expression ')' Statement "rof" ';'.
CompoundStatement = '{' {Statement} '}'.
DeclarationStatement = TypeSpecifier Identifier ';'.
AssignmentStatement = AssignmentExpression ';'.

Expression = [Sign] ( '(' Expression ')' | Literal | Identifier ) { Operator [Sign] ( '(' Expression ')' | Literal | Identifier ) }.

AssignmentExpression = Identifier '=' Expression.

InputStatement = '$' Identifier ';'.
OutputStatement = '@' Expression ';'.

LabelStatement = Identifier ':'.
JumpStatement = 'goto' Identifier ';'.

TypeSpecifier = "int" | "float".

Identifier = Letter {Letter | Digit}.

Literal = IntegerLiteral | FloatingLiteral.
FloatingLiteral = IntegerLiteral '.' [IntegerLiteral].
IntegerLiteral = Digit {Digit}.

Letter = 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z' | 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' | 'V' | 'W' | 'X' | 'Y'.
Digit = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'. 

Sign = '+' | '-'.
Operator = ArithmeticOperator | RelationalOperator.
ArithmeticOperator = '+' | '-' | '*' | "**" | '/' | '%'.
RelationalOperator = '<' | '>' | "<=" | ">=" | "==" | "!=".